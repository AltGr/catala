@@Début métadonnées@@
/*

déclaration structure Personne :
# Pas de données

déclaration structure ParentsGardeAlternée :
  donnée parent1 contenu Personne
  donnée parent2 contenu Personne

déclaration énumération PriseEnCharge :
  -- Complète contenu Personne
  -- GardeAlternée contenu ParentsGardeAlternée

# Une structure regroupe des données ensemble
déclaration structure Enfant :
  donnée fin_obligation_scolaire contenu date
  donnée âge contenu entier
  donnée rémuneration contenu montant
  donnée prise_en_charge contenu PriseEnCharge
  condition confié_service_social

déclaration structure Ménage :
  donnée enfants collection contenu Enfant
  donnée parent_en_charge contenu Personne dépend de Enfant
  donnée enfant_plus_âgé contenu Enfant optionnel
  donnée parents collection contenu Personne
  donnée parent1 contenu Personne
  donnée parent2 contenu Personne optionnel
# Le mot optionnel permet de prévoir le cas où le
# ménage n'a pas d'enfants

déclaration champ d'application MénageBienFormé :
  contexte ménage structure Ménage

champ d'application MénageBienFormé :
  assertion nombre de parents > 0 et nombre de parents <= 2

  # Les champs parent1 et parent2 sont cohérents
  assertion ménage.parent1 dans ménage.parents
  assertion (
    selon ménage.parent2 sous forme
    -- Présent de parent2 :
      parent2 dans ménage.parents et parent2 != ménage.parent1
    -- Absent : vrai
  )

  # Dans la traduction informatique, le ménage est en charge de chacun
  # des enfants
  assertion pour tout enfant dans enfants on a (
    selon enfant.prise_en_charge sous forme
    -- Complète de parent : parent dans ménage.parents
    -- GardeAlternée de parents_garde_alternée :
      parents_garde_alternée.parent1 dans ménage.parents ou
      parents_garde_alternée.parent2 dans ménage.parents
  )

  # parent_en_charge permet de savoir quel parent du ménage
  # est en charge de quel enfant
  définition parent_en_charge de enfant égal à (
    selon enfant.prise_en_charge sous forme
    -- Complète de parent : parent
    -- GardeAlternée de parents_garde_alternée : (
      sous condition parents_garde_alternée.parent1 dans ménage.parents
      conséquence parents_garde_alternée.parent1
      sinon parents_garde_alternée.parent2
  ))

  # Lorsqu'un enfant est confié au service social, il ne peut être
  # en garde alternée
  assertion pour tout enfant dans enfants on a
    sous condition enfant.confié_service_social
    conséquence enfant.prise_en_charge = Complète de SERVICE_SOCIAL

déclaration énumération Prestation:
  -- PrestationAccueilJeuneEnfant
  -- AllocationFamiliale
  -- ComplémentFamilial
  -- AllocationLogement
  -- AllocationÉducationEnfantHandicapé
  -- AllocationSoutienFamilial
  -- AllocationRentréeScolaire
  -- AllocationJournalièrePresenceParentale

déclaration structure TitreI:
  condition droits_ouverts_allocations_familiales dépend de Personne

déclaration structure L511_1:
  donnée prestation_courante contenu Prestation

déclaration structure L512_3 :
  donnée âge_limite_alinéa_2 contenu entier
  donnée âge_limite_alinéa_2_alternatif contenu entier
  donnée plafond_rémunération_alinéa_2 contenu montant
  condition droits_ouverts_prestations_familiales
  condition conditions_hors_âge dépend de Enfant

déclaration structure L521_1 :
  donnée allocations_familiales contenu montant
  condition droits_ouverts_allocations_familiales
  donnée allocation_forfaitaire contenu montant
  condition allocation_forfaitaire_versée
  donnée nombre_minimum_enfants contenu montant
  donnée ressources_ménage contenu montant

déclaration énumération ChargeAllocation :
  -- Complète
  -- Partagée

déclaration énumération ChoixParentAllocataire :
  -- UnParent contenu Personne
  -- DeuxParents

déclaration structure L521_2 :
  donnée récipiendaire_allocations contenu Personne dépend de Enfant
  donnée charge_allocations contenu ChargeAllocation dépend de Enfant
  donnée choix_allocataire_garde_alternée
     contenu ChoixParentAllocataire optionnel
     dépend de Enfant
  condition garde_alternée_unique_allocataire dépend de Enfant
  condition garde_alternée_allocataire_double dépend de Enfant
  condition demande_conjointe_partage_charge_garde_alternée
    dépend de Enfant
  condition desaccord_charge_garde_alternée dépend de Enfant

déclaration structure L521_3 :
  donnée âge_limite_alinéa_1 contenu entier dépend de Enfant
# L'âge limite dépend de l'enfant considéré,
# au contraire des autres données
  donnée minimum_alinéa_2 contenu montant
  condition droits_ouverts_majorations_allocations_familiales
    dépend de Enfant
  donnée majorations_allocations_familiales contenu montant

déclaration structure L551_1 :
  donnée base_mensuelle_prestations_familiales contenu montant

déclaration structure R521_3 :
  donnée allocations_familiales_avec_garde_alternée contenu montant
  donnée rapport_enfants_total_moyen contenu décimal
  donnée nombre_total_enfants contenu entier
  donnée nombre_moyen_enfants contenu décimal

déclaration structure R521_4 :
  donnée majorations_allocation_familiales_avec_garde_alternée
    contenu montant
    dépend de Enfant

déclaration structure D521_1 :
  donnée montant_deuxieme_enfant contenu montant
  donnée montant_troisième_enfant_et_plus contenu montant

déclaration structure D521_3 :
  donnée plafond_I contenu montant
  donnée plafond_II contenu montant

déclaration champ d'application CalculPrestationsFamiliales :
# Les règles déclarées dans PrestationsFamiliales pourront utiliser
# ménage et l512_3 et leur données associées
  contexte ménage structure Ménage
  contexte l512_3 structure L512_3
  contexte l511_1 structure L511_1
inclus champ d'application MénageBienFormé contexte
  PrestationsFamiliales.ménage = MénageBienFormé.ménage

déclaration champ d'application CalculAllocationsFamiliales :
  contexte ménage structure Ménage
  contexte titre_I structure TitreI
  contexte l521_1 structure L521_1
  contexte l521_2 structure L521_2
  contexte l521_3 structure L521_3
  contexte l551_1 structure L551_1
  contexte r521_3 structure R521_4
  contexte r521_4 structure R521_4
  contexte d521_1 structure D521_1
  contexte d521_3 structure D521_3
inclus champ d'application CalculPrestationsFamiliales contexte
   AllocationsFamiliales.ménage = PrestationsFamiliales.ménage
# AllocationsFamiliales est un cas particulier de PrestationsFamiliales,
# le dernier est donc inclus dans l'autre. Il est nécessaire de préciser
# que les deux contextes parlent du même ménage pour caractériser
# l'inclusion.
inclus champ d'application AllocationFamilialesAvril2008

déclaration champ d'application CalculAllocationsFamilialesAvril2008 :
  contexte l521_3 structure L521_3
*/
@@Fin métadonnées@@

@@Code de la sécurité sociale@@

@Article L511-1@
Les prestations familiales comprennent :
1°) la prestation d'accueil du jeune enfant ;
2°) les allocations familiales ;
3°) le complément familial ;
4°) L'allocation de logement régie par les dispositions du livre VIII du code de la construction et de l'habitation ;
5°) l'allocation d'éducation de l'enfant handicapé ;
6°) l'allocation de soutien familial ;
7°) l'allocation de rentrée scolaire ;
8°) (Abrogé) ;
9°) l'allocation journalière de présence parentale.
/*
# Voir métadonnnée L511_1.prestation_courante
*/

@Article L512-3@
Sous réserve des règles particulières à chaque prestation, ouvre droit aux prestations familiales :

1°) tout enfant jusqu'à la fin de l'obligation scolaire ;
/*
champ d'application CalculPrestationsFamiliales :
  règle sous condition
    existe enfant dans ménage.enfants tel que
      date_aujourd_hui <= enfant.fin_obligation_scolaire
  conséquence l512_3.droits_ouverts_prestations_familiales rempli
*/

2°) après la fin de l'obligation scolaire, et jusqu'à un âge limite, tout enfant dont la rémunération éventuelle n'excède pas un plafond.
/*
champ d'application CalculPrestationsFamiliales :
  # On définit les conditions hors âge d'abord car elles
  # sont référencées dans l'article L521-1
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (date_aujourd_hui <= enfant.fin_obligation_scolaire) ou
    (enfant.rémunération < l512_3.plafond_rémunération_alinéa_2)
  conséquence l512_3.conditions_hors_âge de enfant rempli

  règle sous condition
    existe enfant dans ménage.enfants tel que
      (date_aujourd_hui > enfant.fin_obligation_scolaire) et
      (l512_3.conditions_hors_âge de enfant) et
      (enfant.âge < l512_3.âge_limite_alinéa_2)
  conséquence droits_ouverts_prestations_familiales rempli
*/

Toutefois, pour l'attribution du complément familial et de l'allocation de logement mentionnés aux 3° et 4° de l'article L. 511-1, l'âge limite peut être différent de celui mentionné au 2° du présent article.
/*
champ d'application CalculPrestationsFamiliales :
  définition l512_3.âge_limite_alinéa_2 sous condition
    l511_1.prestation_courante = ComplémentFamilial ou
    l511_1.prestation_courante = AllocationLogement
  conséquence égal à
    l512_3.âge_limite_alinéa_2_alternatif
*/

@Article L521-1@
Les allocations familiales sont dues à partir du deuxième enfant à charge.
/*
champ d'application CalculAllocationsFamiliales :
  règle sous condition
    nombre de ménage.enfants >= 2
  conséquence l521_1.droits_ouverts_allocations_familiales rempli

# Les droits doivent être ouverts pour appliquer le champ AllicationsFamiliales
  assertion l521_1.droits_ouverts_allocations_familiales
# Le champ d'application CalculAllocationsFamiliales inclus PrestationsFamiliales,
# mais à condition que la prestation courante soit cohérente
  définition l511_1.prestation_courante égal à AllocationFamiliale
*/

Une allocation forfaitaire par enfant d'un montant fixé par décret est versée pendant un an à la personne ou au ménage qui assume la charge d'un nombre minimum d'enfants également fixé par décret lorsque l'un ou plusieurs des enfants qui ouvraient droit aux allocations familiales atteignent l'âge limite mentionné au 2° de l'article L. 512-3. Cette allocation est versée à la condition que le ou les enfants répondent aux conditions autres que celles de l'âge pour l'ouverture du droit aux allocations familiales.
/*
champ d'application CalculAllocationsFamiliales :
  assertion l521_1.allocation_forfaitaire fixé par décret

  assertion
    l521_1.allocation_forfaitaire_versée si et seulement si
    l521_1.allocation_forfaitaire > 0 €
# L'assertion ci-dessus n'est pas explicitée dans le texte mais
# semble de bon sens, peut-être est-elle précisée dans un autre article ?

  assertion l512_1.nombre_minimum_enfants fixé par décret
  règle sous condition
    existe enfant dans ménage.enfants tel que
      (enfant.age = l512_3.âge_limite_alinéa_2) et
      (l512_3.conditions_hors_âge de enfant)
  conséquence l521_1.allocation_forfaitaire_versée rempli
*/


Le montant des allocations mentionnées aux deux premiers alinéas du présent article, ainsi que celui des majorations mentionnées à l'article L. 521-3 varient en fonction des ressources du ménage ou de la personne qui a la charge des enfants, selon un barème défini par décret.
/*
champ d'application CalculAllocationsFamiliales :
  assertion l512_1.allocations_familiales fixé par décret
  assertion l512_1.allocations_familiales varie avec l521_1.ressources_ménage
  assertion l521_3.majorations_allocations_familiales fixé par décret
  assertion l512_1.majorations_allocations_familiales varie avec
     l521_1.ressources_ménage
*/

Le montant des allocations familiales varie en fonction du nombre d'enfants à charge.
/*
champ d'application CalculAllocationsFamiliales :
  assertion l512_1.allocations_familiales varie avec
     nombre de ménage.enfants
*/

Les niveaux des plafonds de ressources, qui varient en fonction du nombre d'enfants à charge, sont révisés conformément à l'évolution annuelle de l'indice des prix à la consommation, hors tabac.
/*
# Pour formaliser l'évolution des prix, il faudrait recopier ici
# tous les décrets d'application qui fixent la valeur des plafonds
# de ressources. Si cela reste possible à faire pour notre langage,
# nous avons choisi de ne pas inclure tout ce code dans ce document.
*/

Un complément dégressif est versé lorsque les ressources du bénéficiaire dépassent l'un des plafonds, dans la limite de montants définis par décret. Les modalités de calcul de ces montants et celles du complément dégressif sont définies par décret.
/*
# Ditto, le volume du code à inclure pour formaliser cette assertion
# est assez important et nous avons choisi de ne pas l'inclure dans ce
# document.
*/

@Article L521-2@
Les allocations sont versées à la personne qui assume, dans quelques conditions que ce soit, la charge effective et permanente de l'enfant.
/*
champ d'application CalculAllocationsFamiliales :
  définition l521_2.charge_allocations de enfant
    sous condition
      (enfant dans ménage.enfants) et
      (enfant.prise_en_charge sous forme Complète)
    conséquence égal à Complète

  définition l521_2.récipiendaire_allocations de enfant
    sous condition enfant dans ménage.enfants
    conséquence égal à ménage.parent_en_charge
*/

En cas de résidence alternée de l'enfant au domicile de chacun des parents telle que prévue à l'article 373-2-9 du code civil, mise en oeuvre de manière effective, les parents désignent l'allocataire. Cependant, la charge de l'enfant pour le calcul des allocations familiales est partagée par moitié entre les deux parents soit sur demande conjointe des parents, soit si les parents sont en désaccord sur la désignation de l'allocataire. Un décret en Conseil d'État fixe les conditions d'application du présent alinéa.

/*
champ d'application CalculAllocationsFamiliales :
# Premier cas : garde alternée, parents désignent un unique allocataire
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant.prise_en_charge sous forme GardeAlternée) et
    (enfant.choix_allocataire_garde_alternée sous forme Présent) et
    (enfant.choix_allocataire_garde_alternée.Présent sous forme UnParent) et
    (enfant.choix_allocataire_garde_alternée.UnParent dans ménage.parents)
  conséquence (l521_2.garde_alternée_unique_allocataire de enfant) rempli

  définition l521_2.charge_allocations de enfant
    sous condition l521_2.garde_alternée_unique_allocataire de enfant
    conséquence égal à Complète

# Deuxième cas : garde alternée, parents partagent la charge pour
# l'allocation
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant.prise_en_charge sous forme GardeAlternée) et
    (enfant.choix_allocataire_garde_alternée sous forme Présent) et
    (enfant.choix_allocataire_garde_alternée.Présent sous forme DeuxParents)
  conséquence (l521_2.garde_alternée_allocataire_double de enfant) rempli

  définition l521_2.charge_allocations de enfant
    sous condition
      (enfant dans ménage.enfants) et
      (l521_2.garde_alternée_allocataire_double de enfant)
    conséquence égal à Partagée

  définition l521_2.charge_allocations de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant.prise_en_charge sous forme GardeAlternée) et
    (l521_2.demande_conjointe_partage_charge_garde_alternée de enfant ou
     l512_2.desaccord_charge_garde_alternée de enfant)
  conséquence égal à Partagée


# Quelles variables fixées par R521_2 ?
*/

Lorsque la personne qui assume la charge effective et permanente de l'enfant ne remplit pas les conditions prévues au titre I du présent livre pour l'ouverture du droit aux allocations familiales, ce droit s'ouvre du chef du père ou, à défaut, du chef de la mère.
/*
champ d'application CalculAllocationsFamiliales :
  définition l521_2.récipiendaire_allocations de enfant sous condition
    (enfant dans ménage.enfants) et
    (titre_I.droits_ouverts_allocations_familiales de
      (ménage.parent_en_charge de enfant))
  conséquence égal à ménage.parent1
*/

Lorsqu'un enfant est confié au service d'aide sociale à l'enfance, les allocations familiales continuent d'être évaluées en tenant compte à la fois des enfants présents au foyer et du ou des enfants confiés au service de l'aide sociale à l'enfance. La part des allocations familiales dues à la famille pour cet enfant est versée à ce service. Toutefois, le juge peut décider, d'office ou sur saisine du président du conseil général, à la suite d'une mesure prise en application des articles 375-3 et 375-5 du code civil ou des articles 15,16,16 bis et 28 de l'ordonnance n° 45-174 du 2 février 1945 relative à l'enfance délinquante, de maintenir le versement des allocations à la famille, lorsque celle-ci participe à la prise en charge morale ou matérielle de l'enfant ou en vue de faciliter le retour de l'enfant dans son foyer.

/*
champ d'application CalculAllocationsFamiliales :
  définition l521_2.charge_allocations de enfant
    sous condition
      (enfant dans ménage.enfants) et
      (enfant.confié_service_social)
    conséquence égal à Complète

  définition l521_2.récipiendaire_allocations de enfant
    sous condition
      (enfant dans ménage.enfants) et
      (enfant.confié_service_social)
  conséquence égal à SERVICE_SOCIAL
*/

Un décret en Conseil d'Etat fixe les conditions d'application du présent article, notamment dans les cas énumérés ci-dessous :

a) retrait total de l'autorité parentale des parents ou de l'un d'eux ;

b) indignité des parents ou de l'un d'eux ;

c) divorce, séparation de corps ou de fait des parents ;

d) enfants confiés à un service public, à une institution privée, à un particulier.

/*
# Ce programme ne cherche pas à formaliser les détails pour lesquels un enfant
# est confié à un service social.
*/

@Article L521-3@ Chacun des enfants à charge, à l'exception du plus âgé, ouvre droit à partir d'un âge minimum à une majoration des allocations familiales.
/*
champ d'application CalculAllocationsFamiliales :
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant != ménage.enfant_plus_âgé) et
    (enfant.âge >= l521_3.âge_limite_alinéa_1 de enfant)
  conséquence
    l521_3.droits_ouverts_majorations_allocations_familiales de enfant
  rempli
*/

Toutefois, les personnes ayant un nombre déterminé d'enfants à charge bénéficient de ladite majoration pour chaque enfant à charge à partir de l'âge mentionné au premier alinéa.
/*
champ d'application CalculAllocationsFamiliales :
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (nombre de ménage.enfants >= l512_3.minimum_alinéa_2) et
    (enfant.âge >= l521_3.âge_limite_alinéa_1 de enfant)
  conséquence
    l521_3.droits_ouverts_majorations_allocations_familiales de enfant
  rempli
*/

@Article L.551-1@
Le montant des prestations familiales est déterminé d'après des bases mensuelles de calcul revalorisées au 1er avril de chaque année par application du coefficient mentionné à l'article L. 161-25.
/*
# Voir déclaration structure L551_1 dans les métadonnées. Idem que L521-1,
# on ne formalise pas ici l'évolution de la BAMF
*/

@Article R521-1@ L'âge mentionné au premier alinéa de l'article L. 521-3 à partir duquel les enfants ouvrent droit à la majoration des allocations familiales est fixé à 14 ans.
/*
champ d'application CalculAllocationsFamiliales :
  définition l521_3.âge_limite_alinéa_1 de enfant égal à 14 an
*/

Le nombre minimum d'enfants à charge, mentionné au deuxième alinéa de l'article L. 521-3 ouvrant droit à ladite majoration pour chaque enfant est fixé à trois.
/*
champ d'application CalculAllocationsFamiliales :
  définition l521_3.minimum_alinéa_2
    égal à 3
*/

NOTA : Décret n° 2008-409 du 28 avril 2008 JORF du 29 avril 2008 art. 2 : Les modifications induites par le décret n° 2008-409 s'appliquent aux enfants dont le onzième anniversaire est postérieur au 30 avril 2008.
/*
# Notons ici un champ d'application différent, correspondant à une ancienne
# version du corpus législatif dont un morceau s'applique encore. Nous avons
# choisi de montrer ce vieux champ d'application ici plutôt qu'à côté du texte
# du décret de 2008 pour des raisons de place seulement.
champ d'application CalculAllocationFamilialesAvril2008:
# Âge limite avant décret n° 2008-409 du 28 avril 2008
  définition l521_3.minimum_alinéa_2 de enfant égal à 16 an

champ d'application CalculAllocationsFamiliales :
  définition l521_3.minimum_alinéa_2 de enfant
  sous condition (enfant.date_naissance + 11 an <= 30/04/2008)
  conséquence égal à
    CalculAllocationFamilialesAvril2008.l521_3.minimum_alinéa_2 de enfant
*/

@Article R521-2@
Dans les situations visées au deuxième alinéa de l'article L. 521-2, l'allocataire est celui des deux parents qu'ils désignent d'un commun accord. A défaut d'accord sur la désignation d'un allocataire unique, chacun des deux parents peut se voir reconnaître la qualité d'allocataire :
1° Lorsque les deux parents en ont fait la demande conjointe ;
2° Lorsque les deux parents n'ont ni désigné un allocataire unique, ni fait une demande conjointe de partage.
/*
# Le fait que l'allocataire dans le cas d'un commun accord soit celui des
# deux parents désigné par l'accord est déjà modélisé dans L512-2.

champ d'application CalculAllocationsFamiliales :
  définition l521_2.charge_allocations de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant.prise_en_charge sous forme GardeAlternée) et
    (
      (l521_2.demande_conjointe_partage_charge_garde_alternée) ou
      (
        (non l521_2.demande_conjointe_partage_charge_garde_alternée) et
        (enfant.choix_allocataire_garde_alternée sous forme Absent)
      )
    )
  conséquence égal à Partagée
*/

Lorsque les parents ont désigné un allocataire unique ou fait une demande conjointe de partage, ils ne peuvent remettre en cause les modalités ainsi choisies qu'au bout d'un an, sauf modification des modalités de résidence du ou des enfants.
/*
# On ne modélise pas le délai minimal entre changement de situation dans
# ce programme.
*/

@Article R521-3@
Sous réserve de l'article R. 521-4, dans les situations visées aux 1° et 2° de l'article R. 521-2, la prestation due à chacun des parents est égale au montant des allocations familiales dues pour le total des enfants à charge, multiplié par un coefficient résultant du rapport entre le nombre moyen d'enfants et le nombre total d'enfants.

/*
champ d'application CalculAllocationsFamiliales :
  définition r521_3.allocations_familiales_avec_garde_alternée égal à
    l521_1.allocation_familiales *
    r521_3.rapport_enfants_total_moyen

  définition r521_3.rapport_enfants_total_moyen égal à
    r521_3.nombre_moyen_enfants / r521_3.nombre_total_enfants
*/

Le nombre moyen d'enfants, pour chaque foyer, est obtenu en faisant la somme du nombre d'enfants à charge dans les conditions suivantes :
1° Chaque enfant en résidence alternée compte pour 0,5 ;
2° Les autres enfants à charge comptent pour 1.
/*
champ d'application CalculAllocationsFamiliales :
  définition r521_3.nombre_moyen_enfants égal à
    somme pour enfant dans ménage.enfants de (
      selon enfant.prise_en_charge sous forme
      -- Complète : 1
      -- GardeAlternée de parents : 0.5
    )
*/
Le nombre total d'enfants, pour chaque foyer, est obtenu en faisant la somme du ou des enfants en résidence alternée et, le cas échéant, du ou des autres enfants à charge.
/*
champ d'application CalculAllocationsFamiliales :
  définition r521_3.nombre_total_enfants égal à
    nombre de ménage.enfants
*/

@Article R521-4@
Pour l'ouverture du droit à la majoration prévue à l'article L. 521-3, le nombre d'enfants à charge est évalué dans les conditions prévues au premier alinéa de l'article R. 521-3.

/*
# ménage.enfants ne contient que des enfants à charge donc rien à formaliser
# ici
*/

Lorsque le ou les enfants ouvrant droit à ladite majoration sont en résidence alternée, le montant servi au titre de cette majoration est réduit de moitié.

/*
champ d'application CalculAllocationsFamiliales :
  définition
    r521_4.majorations_allocation_familiales_avec_garde_alternée de enfant
  sous condition enfant dans ménage.enfants
  conséquence égal à (
    selon enfant.prise_en_charge sous forme
    -- Complète : l521_3.majorations_allocation_familiales
    -- GardeAlternée de parents: l521_3.majorations_allocation_familiales / 2
  )
*/

@Article D521-1@
I.- Pour l'application de l'article L. 521-1, le montant des allocations familiales et de la majoration pour âge prévue à l'article L. 521-3 est défini selon le barème suivant :

1° Lorsque le ménage ou la personne a disposé d'un montant de ressources inférieur ou égal au plafond défini au I de l'article D. 521-3, les taux servant au calcul des allocations familiales sont fixés, en pourcentage de la base mensuelle prévue à l'article L. 551-1, à :
a) 32 % pour le deuxième enfant à charge ;
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_1.montant_deuxieme_enfant égal à
    sous condition nombre de ménage.enfants >= 2
    conséquence l551_1.base_mensuelle_prestations_familiales * 32 %
    sinon 0 €
*/
b) 41 % pour le troisième enfant à charge et chacun des suivants.
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_1.montant_troisième_enfant_et_plus égal à
    sous condition nombre de ménage.enfants >= 3
    conséquence l551_1.base_mensuelle_prestations_familiales * 41 % * (
      (nombre de ménage.enfants) - 2
    ) sinon 0 €
*/
La majoration pour âge est fixée à 16 % de la base mensuelle de calcul des prestations familiales ;
/*
champ d'application CalculAllocationsFamiliales :
   définition d521_1.majoration_pour_âge égal à
     l551_1.base_mensuelle_prestations_familiales * 16 %
*/
2° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au I de l'article D. 521-3 et inférieur ou égal à celui défini au II du même article, les taux servant au calcul des allocations familiales sont fixés, en pourcentage de la base mensuelle prévue à l'article L. 551-1, à :
a) 16 % pour le deuxième enfant à charge ;
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_1.montant_deuxieme_enfant sous condition
    (l521_1.ressources_ménage > d521_3.plafond_I) et
    (l521_1.ressources_ménage <= d521_3.plafond_II)
  conséquence égal à
    sous condition nombre de ménage.enfants >= 2
    conséquence l551_1.base_mensuelle_prestations_familiales * 32 %
    sinon 0 €
*/
b) 20,5 % pour le troisième enfant à charge et chacun des suivants.
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_1.montant_troisième_enfant_et_plus sous condition
    (l521_1.ressources_ménage > d521_3.plafond_I) et
    (l521_1.ressources_ménage <= d521_3.plafond_II)
  conséquence égal à
    sous condition nombre de ménage.enfants >= 3
    conséquence l551_1.base_mensuelle_prestations_familiales * 20.5 % * (
      (nombre de ménage.enfants) - 2
    ) sinon 0 €
*/
La majoration pour âge est fixée à 8 % de la base mensuelle de calcul des prestations familiales ;
/*
champ d'application CalculAllocationsFamiliales :
   définition d521_1.majoration_pour_âge sous condition
     (l521_1.ressources_ménage > d521_3.plafond_I) et
     (l521_1.ressources_ménage <= d521_3.plafond_II)
   conséquence égal à
     l551_1.base_mensuelle_prestations_familiales * 8 %
*/

3° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au II de l'article D. 521-3, les taux servant au calcul des allocations familiales sont fixés, en pourcentage de la base mensuelle prévue à l'article L. 551-1, à :
a) 8 % pour le deuxième enfant à charge ;
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_1.montant_deuxieme_enfant sous condition
    (l521_1.ressources_ménage > d521_3.plafond_II)
  conséquence égal à
    sous condition nombre de ménage.enfants >= 2
    conséquence l551_1.base_mensuelle_prestations_familiales * 8 %
    sinon 0 €
*/
b) 10,25 % pour le troisième enfant à charge et chacun des suivants.
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_1.montant_troisième_enfant_et_plus sous condition
    (l521_1.ressources_ménage > d521_3.plafond_II)
  conséquence égal à
    sous condition nombre de ménage.enfants >= 3
    conséquence l551_1.base_mensuelle_prestations_familiales * 10.25 % * (
      (nombre de ménage.enfants) - 2
    ) sinon 0 €
*/
La majoration pour âge est fixée à 4 % de la base mensuelle de calcul des prestations familiales.
/*
champ d'application CalculAllocationsFamiliales :
   définition d521_1.majoration_pour_âge sous condition
     (l521_1.ressources_ménage > d521_3.plafond_II)
   conséquence égal à
     l551_1.base_mensuelle_prestations_familiales * 4 %
*/

II.- En application du sixième alinéa de l'article L. 521-1, le montant mensuel des allocations familiales et, le cas échéant, de la majoration pour âge est majoré d'un complément dégressif, lorsque les ressources annuelles du ménage ou de la personne dépassent l'un des plafonds défini au I ou au II de l'article D. 521-3 d'une somme inférieure à douze fois le montant mensuel des allocations familiales augmenté, le cas échéant, de la ou des majorations pour âge.

Ce complément dégressif est égal, pour chaque mois, au douzième de la différence entre, d'une part, ce plafond de ressources majoré de la somme définie à l'alinéa précédent et, d'autre part, le montant des ressources.
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_1.complément_dégressif de allocation
   sous condition
     (l521_1.ressources_ménage > d521_3.plafond_I) et
     (l521_1.ressources_ménage <= d521_3.plafond_I + 12 * allocation)
   conséquence égal à
     (d521_3.plafond_I + 12 * allocation - l521_1.ressources_ménage) / 12

champ d'application CalculAllocationsFamiliales :
 définition d521_1.complément_dégressif de allocation
  sous condition
    (l521_1.ressources_ménage > d521_3.plafond_II) et
    (l521_1.ressources_ménage <= d521_3.plafond_II + 12 * allocation)
  conséquence égal à
    (d521_3.plafond_II + 12 * allocation - l521_1.ressources_ménage) / 12
*/

@Article D521-2@
I.- Le montant mensuel de l'allocation forfaitaire prévue au deuxième alinéa de l'article L. 521-1 est défini selon le barème suivant :

1° Lorsque le ménage ou la personne a disposé d'un montant de ressources inférieur ou égal au plafond défini au I de l'article D. 521-3, le montant mensuel de l'allocation forfaitaire est fixé à 20,234 % de la base mensuelle de calcul des allocations familiales par enfant ;

2° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au I de l'article D. 521-3 et inférieur ou égal à celui défini au II du même article, le montant mensuel de l'allocation forfaitaire est fixé à 10,117 % de la base mensuelle de calcul des allocations familiales par enfant ;

3° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au II de l'article D. 521-3, le montant mensuel de l'allocation forfaitaire est fixé à 5,059 % de la base mensuelle de calcul des allocations familiales par enfant.

II.- En application du sixième alinéa de l'article L. 521-1, le montant mensuel de l'allocation forfaitaire est majoré d'un complément dégressif lorsque les ressources annuelles du ménage ou de la personne dépassent l'un des plafonds défini au I ou au II de l'article D. 521-3 d'une somme inférieure à douze fois le montant mensuel de l'allocation forfaitaire auquel l'enfant ouvre droit.
Ce complément dégressif est égal, pour chaque mois, au douzième de la différence entre, d'une part, ce plafond de ressources majoré de la somme définie à l'alinéa précédent et, d'autre part, le montant des ressources.

III.- Le nombre minimum d'enfants à charge mentionné au deuxième alinéa de l'article L. 521-1 est fixé à trois.
