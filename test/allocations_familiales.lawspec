@@Début métadonnées@@
/*

déclaration structure Personne :
# Pas de données

déclaration structure ParentsGardeAlternée :
  donnée parent1 contenu Personne
  donnée parent2 contenu Personne

déclaration énumération PriseEnCharge :
  -- Complète contenu Personne
  -- GardeAlternée contenu ParentsGardeAlternée

# Une structure regroupe des données ensemble
déclaration structure Enfant :
  donnée fin_obligation_scolaire contenu date
  donnée âge contenu entier
  donnée rémuneration contenu montant
  donnée prise_en_charge contenu PriseEnCharge
  condition confié_service_social

déclaration structure Ménage :
  donnée enfants contenu collection Enfant
  donnée parent_en_charge contenu Personne dépend de Enfant
  donnée enfant_plus_âgé contenu Enfant optionnel
  donnée parents contenu collection Personne
  donnée parent1 contenu Personne
  donnée parent2 contenu Personne optionnel
  # Le mot optionnel permet de prévoir le cas où le
  # ménage n'a pas d'enfants

déclaration champ d'application MénageBienFormé :
  contexte ménage contenu Ménage

champ d'application MénageBienFormé :
  assertion nombre de parents > 0 et nombre de parents <= 2

  # Les champs parent1 et parent2 sont cohérents
  assertion ménage.parent1 dans ménage.parents
  assertion (
    selon ménage.parent2 sous forme
    -- Présent de parent2 :
      parent2 dans ménage.parents et parent2 != ménage.parent1
    -- Absent : vrai
  )

  # Dans la traduction informatique, le ménage est en charge de chacun
  # des enfants
  assertion (pour tout enfant dans enfants on a
    selon enfant.prise_en_charge sous forme
    -- Complète de parent : parent dans ménage.parents
    -- GardeAlternée de parents_garde_alternée :
      parents_garde_alternée.parent1 dans ménage.parents ou
      parents_garde_alternée.parent2 dans ménage.parents
  )

  # parent_en_charge permet de savoir quel parent du ménage
  # est en charge de quel enfant
  définition parent_en_charge de enfant égal à (
    selon enfant.prise_en_charge sous forme
    -- Complète de parent : parent
    -- GardeAlternée de parents_garde_alternée : (
      si parents_garde_alternée.parent1 dans ménage.parents
      alors parents_garde_alternée.parent1
      sinon parents_garde_alternée.parent2
  ))

  # Lorsqu'un enfant est confié au service social, il ne peut être
  # en garde alternée
  assertion pour tout enfant dans enfants on a
    si enfant.confié_service_social
    alors enfant.prise_en_charge = Complète contenu service_social
    sinon vrai

déclaration énumération Prestation:
  -- PrestationAccueilJeuneEnfant
  -- AllocationFamiliale
  -- ComplémentFamilial
  -- AllocationLogement
  -- AllocationÉducationEnfantHandicapé
  -- AllocationSoutienFamilial
  -- AllocationRentréeScolaire
  -- AllocationJournalièrePresenceParentale

déclaration structure PrestationsFamiliales :
  condition conditions_hors_âge dépend de Enfant
  condition droits_ouverts dépend de Enfant

déclaration énumération ChargeAllocation :
  -- Complète
  -- Partagée

déclaration structure AllocationsFamiliales :
  condition droits_ouverts
  condition conditions_hors_âge dépend de Enfant
  donnée base contenu montant
  donnée avec_garde_alternée contenu montant
  donnée avec_garde_alternée_et_complément contenu montant
  donnée montant_versé contenu montant
  donnée récipiendaire_par_enfant contenu Personne dépend de Enfant
  donnée charge_par_enfant contenu ChargeAllocation dépend de Enfant
  donnée rapport_enfants_total_moyen contenu décimal
  donnée nombre_total_enfants contenu entier
  donnée nombre_moyen_enfants contenu décimal
  donnée montant_deuxieme_enfant contenu montant
  donnée montant_troisième_enfant_et_plus contenu montant

déclaration structure AllocationForfaitaire :
  condition droits_ouverts dépend de Enfant
  donnée base contenu montant
  donnée avec_complément contenu montant
  donnée montant_versé contenu montant

déclaration énumération ChoixParentAllocataire :
  -- UnParent contenu Personne
  -- DeuxParents

déclaration structure AllocationsGardeAlternée :
  donnée choix_allocataire contenu ChoixParentAllocataire optionnel
     dépend de Enfant
  condition unique_allocataire dépend de Enfant
  condition allocataire_double dépend de Enfant
  condition demande_conjointe_partage_charge dépend de Enfant
  condition desaccord_charge dépend de Enfant

déclaration structure MajorationsAllocationsFamiliales :
  condition droits_ouverts dépend de Enfant
  donnée base_par_enfant contenu montant dépend de Enfant
  donnée avec_garde_alternée contenu montant dépend de Enfant
  donnée avec_garde_alternée_et_complément contenu montant dépend de Enfant
  donnée montant_versé contenu montant dépend de Enfant

déclaration structure TitreI:
  condition droits_ouverts_allocations_familiales dépend de Personne

déclaration structure L511_1:
  donnée prestation_courante contenu Prestation

déclaration structure L512_3 :
  donnée âge_limite_alinéa_2 contenu entier
  donnée âge_limite_alinéa_2_alternatif contenu entier
  condition âge_limite_alinéa_2_alternatif_utilisé
  donnée plafond_rémunération_alinéa_2 contenu montant

déclaration structure L521_1 :
  donnée nombre_minimum_enfants contenu montant
  donnée ressources_ménage contenu montant

déclaration structure L521_3 :
  donnée âge_limite_alinéa_1 contenu entier dépend de Enfant
  donnée minimum_alinéa_2 contenu montant

déclaration structure L551_1 :
  donnée base_mensuelle_prestations_familiales contenu montant

déclaration structure D521_1 :
  donnée complément_dégressif contenu montant dépend de montant

déclaration structure D521_3 :
  donnée plafond_I contenu montant
  donnée plafond_II contenu montant

déclaration champ d'application CalculPrestationsFamiliales :
  # Les règles déclarées dans PrestationsFamiliales pourront utiliser
  # ménage et l512_3 et leur données associées
  contexte ménage contenu Ménage
  contexte prestations_familiales contenu PrestationsFamiliales
  contexte l511_1 contenu L511_1
  contexte l512_3 contenu L512_3
inclus champ d'application MénageBienFormé contexte
  PrestationsFamiliales.ménage = MénageBienFormé.ménage

déclaration champ d'application CalculAllocationsFamiliales :
  contexte ménage contenu Ménage
  contexte allocations_familiales contenu AllocationsFamiliales
  contexte allocation_forfaitaire contenu AllocationForfaitaire
  contexte allocations_garde_alternée contenu AllocationsGardeAlternée
  contexte majorations_allocations_familiales
    contenu MajorationsAllocationsFamiliales

  contexte titre_I contenu TitreI
  contexte l521_1 contenu L521_1
  contexte l521_3 contenu L521_3
  contexte l551_1 contenu L551_1
  contexte d521_1 contenu D521_1
  contexte d521_3 contenu D521_3
inclus champ d'application CalculPrestationsFamiliales contexte
  AllocationsFamiliales.ménage = PrestationsFamiliales.ménage
  # AllocationsFamiliales est un cas particulier de PrestationsFamiliales,
  # le dernier est donc inclus dans l'autre. Il est nécessaire de préciser
  # que les deux contextes parlent du même ménage pour caractériser
  # l'inclusion.
inclus champ d'application AllocationFamilialesAvril2008

déclaration champ d'application CalculAllocationsFamilialesAvril2008 :
  contexte l521_3 contenu L521_3
*/
@@Fin métadonnées@@

@@Code de la sécurité sociale@@

@Article L511-1@
Les prestations familiales comprennent :
1°) la prestation d'accueil du jeune enfant ;
2°) les allocations familiales ;
3°) le complément familial ;
4°) L'allocation de logement régie par les dispositions du livre VIII du code de la construction et de l'habitation ;
5°) l'allocation d'éducation de l'enfant handicapé ;
6°) l'allocation de soutien familial ;
7°) l'allocation de rentrée scolaire ;
8°) (Abrogé) ;
9°) l'allocation journalière de présence parentale.
/*
# Voir métadonnnée L511_1.prestation_courante
*/

@Article L512-3@
Sous réserve des règles particulières à chaque prestation, ouvre droit aux prestations familiales :
/*
champ d'application CalculPrestationsFamiliales :
  # On suppose dans le programme  que tous les enfants déclarés dans
  # ménage.enfants ouvrent droit aux allocations familiales
  assertion pour tout enfant dans ménage.enfants on a
    prestations_familiales.droits_ouverts de enfant
*/

1°) tout enfant jusqu'à la fin de l'obligation scolaire ;
/*
champ d'application CalculPrestationsFamiliales :
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (date_aujourd_hui <= enfant.fin_obligation_scolaire)
  conséquence prestations_familiales.droits_ouverts de enfant rempli
*/

2°) après la fin de l'obligation scolaire, et jusqu'à un âge limite, tout enfant dont la rémunération éventuelle n'excède pas un plafond.
/*
champ d'application CalculPrestationsFamiliales :
  # On définit les conditions hors âge d'abord car elles
  # sont référencées dans l'article L521-1
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et (
      (date_aujourd_hui <= enfant.fin_obligation_scolaire) ou
      (enfant.rémunération < l512_3.plafond_rémunération_alinéa_2)
    )
  conséquence prestations_familiales.conditions_hors_âge de enfant rempli

  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (date_aujourd_hui > enfant.fin_obligation_scolaire) et
    (l512_3.conditions_hors_âge de enfant) et
    (enfant.âge < l512_3.âge_limite_alinéa_2)
  conséquence prestations_familiales.droits_ouverts de enfant rempli
*/

Toutefois, pour l'attribution du complément familial et de l'allocation de logement mentionnés aux 3° et 4° de l'article L. 511-1, l'âge limite peut être différent de celui mentionné au 2° du présent article.
/*
champ d'application CalculPrestationsFamiliales :
  définition l512_3.âge_limite_alinéa_2 sous condition
    l512_3.âge_limite_alinéa_2_alternatif_utilisé et
    (l511_1.prestation_courante = ComplémentFamilial ou
     l511_1.prestation_courante = AllocationLogement)
  conséquence égal à
    l512_3.âge_limite_alinéa_2_alternatif
*/

@Article L521-1@
Les allocations familiales sont dues à partir du deuxième enfant à charge.
/*
champ d'application CalculAllocationsFamiliales :
  règle sous condition
    nombre de ménage.enfants >= 2
  conséquence allocations_familiales.droits_ouverts rempli

  # Les droits doivent être ouverts pour appliquer le champ
  # CalculAllocationsFamiliales. Si cette condition n'est pas remplie,
  # alors il est inutile de calculer le reste
  assertion allocations_familiales.droits_ouverts
  # Le champ d'application CalculAllocationsFamiliales inclus
  # PrestationsFamiliales, mais à condition que la prestation courante
  # soit cohérente
  définition l511_1.prestation_courante égal à AllocationFamiliale
*/

Une allocation forfaitaire par enfant d'un montant fixé par décret est versée pendant un an à la personne ou au ménage qui assume la charge d'un nombre minimum d'enfants également fixé par décret lorsque l'un ou plusieurs des enfants qui ouvraient droit aux allocations familiales atteignent l'âge limite mentionné au 2° de l'article L. 512-3. Cette allocation est versée à la condition que le ou les enfants répondent aux conditions autres que celles de l'âge pour l'ouverture du droit aux allocations familiales.
/*
champ d'application CalculAllocationsFamiliales :
  assertion allocation_forfaitaire.montant_versé fixé par décret

  # Ici, l'ouverture du droit aux allocations familiales est conditionné
  # au fait d'avoir deux enfants à charges qui répondent aux conditions
  # d'ouverture du droit à une prestation familiale. Pour cette raison,
  # nous nous référrons ici aux conditions_hors_âge de prestations_familiales
  # et non pas de allocations_familiales
  règle dépend de enfant sous condition
    prestations_familiales.conditions_hors_âge de enfant
  conséquence allocations_familiales.conditions_hors_âge de enfant rempli

  règle dépend de enfant sous condition
    (nombre de ménage.enfants >= l512_1.nombre_minimum_enfants) et
    (enfant dans ménage.enfants) et
    (enfant.âge = l512_3.âge_limite_alinéa_2) et
    (allocations_familiales.conditions_hors_âge de enfant)
  conséquence allocation_forfaitaire.droits_ouverts de enfant rempli
*/


Le montant des allocations mentionnées aux deux premiers alinéas du présent article, ainsi que celui des majorations mentionnées à l'article L. 521-3 varient en fonction des ressources du ménage ou de la personne qui a la charge des enfants, selon un barème défini par décret.
/*
champ d'application CalculAllocationsFamiliales :
  assertion allocations_familiales.montant_versé fixé par décret
  assertion allocations_familiales.montant_versé varie avec
     l521_1.ressources_ménage
  assertion majorations_allocations_familiales.montant_versé fixé par décret
  assertion majorations_allocations_familiales.montant_versé varie avec
     l521_1.ressources_ménage
*/

Le montant des allocations familiales varie en fonction du nombre d'enfants à charge.
/*
champ d'application CalculAllocationsFamiliales :
  assertion allocations_familiales.montant_versé varie avec
     nombre de ménage.enfants
*/

Les niveaux des plafonds de ressources, qui varient en fonction du nombre d'enfants à charge, sont révisés conformément à l'évolution annuelle de l'indice des prix à la consommation, hors tabac.
/*
# Pour formaliser l'évolution des prix, il faudrait recopier ici
# tous les décrets d'application qui fixent la valeur des plafonds
# de ressources. Si cela reste possible à faire pour notre langage,
# nous avons choisi de ne pas inclure tout ce code dans ce document.
*/

Un complément dégressif est versé lorsque les ressources du bénéficiaire dépassent l'un des plafonds, dans la limite de montants définis par décret. Les modalités de calcul de ces montants et celles du complément dégressif sont définies par décret.
/*
# Ditto, le volume du code à inclure pour formaliser cette assertion
# est assez important et nous avons choisi de ne pas l'inclure dans ce
# document.
*/

@Article L521-2@
Les allocations sont versées à la personne qui assume, dans quelques conditions que ce soit, la charge effective et permanente de l'enfant.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.charge_par_enfant de enfant
    sous condition
      (enfant dans ménage.enfants) et
      (enfant.prise_en_charge sous forme Complète)
    conséquence égal à Complète

  définition allocations_familiales.récipiendaire_par_enfant de enfant
    sous condition enfant dans ménage.enfants
    conséquence égal à ménage.parent_en_charge
*/

En cas de résidence alternée de l'enfant au domicile de chacun des parents telle que prévue à l'article 373-2-9 du code civil, mise en oeuvre de manière effective, les parents désignent l'allocataire. Cependant, la charge de l'enfant pour le calcul des allocations familiales est partagée par moitié entre les deux parents soit sur demande conjointe des parents, soit si les parents sont en désaccord sur la désignation de l'allocataire. Un décret en Conseil d'État fixe les conditions d'application du présent alinéa.

/*
champ d'application CalculAllocationsFamiliales :
# Premier cas : garde alternée, parents désignent un unique allocataire
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant.prise_en_charge sous forme GardeAlternée) et
    ((allocations_garde_alternée.choix_allocataire de enfant)
      sous forme Présent) et
    ((allocations_garde_alternée.choix_allocataire de enfant) -> Présent
      sous forme UnParent) et
    (((allocations_garde_alternée.choix_allocataire de enfant) -> Présent -> UnParent)
      dans ménage.parents)
  conséquence allocations_garde_alternée.unique_allocataire de enfant rempli

  définition allocations_familiales.charge_par_enfant de enfant
    sous condition allocations_garde_alternée.unique_allocataire de enfant
    conséquence égal à Complète

# Deuxième cas : garde alternée, parents partagent la charge pour
# l'allocation
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant.prise_en_charge sous forme GardeAlternée) et
    (enfant.choix_allocataire_garde_alternée sous forme Présent) et
    (enfant.choix_allocataire_garde_alternée -> Présent sous forme DeuxParents)
  conséquence allocations_garde_alternée.allocataire_double de enfant rempli

  définition allocations_familiales.charge_par_enfant de enfant
    sous condition
      (enfant dans ménage.enfants) et
      (allocations_garde_alternée.allocataire_double de enfant)
    conséquence égal à Partagée

  définition allocations_familiales.charge_par_enfant de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant.prise_en_charge sous forme GardeAlternée) et
    (allocations_garde_alternée.demande_conjointe_partage_charge de enfant ou
     allocations_garde_alternée.desaccord_charge de enfant)
  conséquence égal à Partagée


# Quelles variables fixées par R521_2 ?
*/

Lorsque la personne qui assume la charge effective et permanente de l'enfant ne remplit pas les conditions prévues au titre I du présent livre pour l'ouverture du droit aux allocations familiales, ce droit s'ouvre du chef du père ou, à défaut, du chef de la mère.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.récipiendaire_par_enfant de enfant
  sous condition
    (enfant dans ménage.enfants) et
    (titre_I.droits_ouverts_allocations_familiales de
      (ménage.parent_en_charge de enfant))
  conséquence égal à ménage.parent1
  # ménage.parent1 est le père ou à défaut la mère
  # (ménage.parent2 est optionnel)
*/

Lorsqu'un enfant est confié au service d'aide sociale à l'enfance, les allocations familiales continuent d'être évaluées en tenant compte à la fois des enfants présents au foyer et du ou des enfants confiés au service de l'aide sociale à l'enfance. La part des allocations familiales dues à la famille pour cet enfant est versée à ce service. Toutefois, le juge peut décider, d'office ou sur saisine du président du conseil général, à la suite d'une mesure prise en application des articles 375-3 et 375-5 du code civil ou des articles 15,16,16 bis et 28 de l'ordonnance n° 45-174 du 2 février 1945 relative à l'enfance délinquante, de maintenir le versement des allocations à la famille, lorsque celle-ci participe à la prise en charge morale ou matérielle de l'enfant ou en vue de faciliter le retour de l'enfant dans son foyer.

/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.charge_par_enfant de enfant
    sous condition
      (enfant dans ménage.enfants) et
      (enfant.confié_service_social)
    conséquence égal à Complète

  définition allocations_familiales.récipiendaire_par_enfant de enfant
    sous condition
      (enfant dans ménage.enfants) et
      (enfant.confié_service_social)
  conséquence égal à service_social
*/

Un décret en Conseil d'Etat fixe les conditions d'application du présent article, notamment dans les cas énumérés ci-dessous :

a) retrait total de l'autorité parentale des parents ou de l'un d'eux ;

b) indignité des parents ou de l'un d'eux ;

c) divorce, séparation de corps ou de fait des parents ;

d) enfants confiés à un service public, à une institution privée, à un particulier.

/*
# Ce programme ne cherche pas à formaliser les détails pour lesquels un enfant
# est confié à un service social.
*/

@Article L521-3@ Chacun des enfants à charge, à l'exception du plus âgé, ouvre droit à partir d'un âge minimum à une majoration des allocations familiales.
/*
champ d'application CalculAllocationsFamiliales :
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant != ménage.enfant_plus_âgé) et
    (enfant.âge >= l521_3.âge_limite_alinéa_1 de enfant)
  conséquence
    majorations_allocations_familiales.droits_ouverts de enfant
  rempli
*/

Toutefois, les personnes ayant un nombre déterminé d'enfants à charge bénéficient de ladite majoration pour chaque enfant à charge à partir de l'âge mentionné au premier alinéa.
/*
champ d'application CalculAllocationsFamiliales :
  règle dépend de enfant sous condition
    (enfant dans ménage.enfants) et
    (nombre de ménage.enfants >= l521_3.minimum_alinéa_2) et
    (enfant.âge >= l521_3.âge_limite_alinéa_1 de enfant)
  conséquence
    majorations_allocations_familiales.droits_ouverts de enfant
  rempli
*/

@Article L.551-1@
Le montant des prestations familiales est déterminé d'après des bases mensuelles de calcul revalorisées au 1er avril de chaque année par application du coefficient mentionné à l'article L. 161-25.
/*
# Voir déclaration structure L551_1 dans les métadonnées. Idem que L521-1,
# on ne formalise pas ici l'évolution de la BAMF
*/

@Article R521-1@ L'âge mentionné au premier alinéa de l'article L. 521-3 à partir duquel les enfants ouvrent droit à la majoration des allocations familiales est fixé à 14 ans.
/*
champ d'application CalculAllocationsFamiliales :
  définition l521_3.âge_limite_alinéa_1 de enfant égal à 14 an
*/

Le nombre minimum d'enfants à charge, mentionné au deuxième alinéa de l'article L. 521-3 ouvrant droit à ladite majoration pour chaque enfant est fixé à trois.
/*
champ d'application CalculAllocationsFamiliales :
  définition l521_3.minimum_alinéa_2 égal à 3
*/

NOTA : Décret n° 2008-409 du 28 avril 2008 JORF du 29 avril 2008 art. 2 : Les modifications induites par le décret n° 2008-409 s'appliquent aux enfants dont le onzième anniversaire est postérieur au 30 avril 2008.
/*
# Notons ici un champ d'application différent, correspondant à une ancienne
# version du corpus législatif dont un morceau s'applique encore. Nous avons
# choisi de montrer ce vieux champ d'application ici plutôt qu'à côté du texte
# du décret de 2008 pour des raisons de place seulement.
champ d'application CalculAllocationFamilialesAvril2008:
# Âge limite avant décret n° 2008-409 du 28 avril 2008
  définition l521_3.minimum_alinéa_2 de enfant égal à 16 an

champ d'application CalculAllocationsFamiliales :
  définition l521_3.minimum_alinéa_2 de enfant
  sous condition (enfant.date_naissance + 11 an <= 30/04/2008)
  conséquence égal à
    CalculAllocationFamilialesAvril2008.l521_3.minimum_alinéa_2 de enfant
*/

@Article R521-2@
Dans les situations visées au deuxième alinéa de l'article L. 521-2, l'allocataire est celui des deux parents qu'ils désignent d'un commun accord. A défaut d'accord sur la désignation d'un allocataire unique, chacun des deux parents peut se voir reconnaître la qualité d'allocataire :
1° Lorsque les deux parents en ont fait la demande conjointe ;
2° Lorsque les deux parents n'ont ni désigné un allocataire unique, ni fait une demande conjointe de partage.
/*
# Le fait que l'allocataire dans le cas d'un commun accord soit celui des
# deux parents désigné par l'accord est déjà modélisé dans L512-2.

champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.charge_par_enfant de enfant sous condition
    (enfant dans ménage.enfants) et
    (enfant.prise_en_charge sous forme GardeAlternée) et
    (
      (allocations_garde_alternée.demande_conjointe_partage_charge) ou
      (
        (non allocations_garde_alternée.demande_conjointe_partage_charge) et
        (enfant.choix_allocataire_garde_alternée sous forme Absent)
      )
    )
  conséquence égal à Partagée
*/

Lorsque les parents ont désigné un allocataire unique ou fait une demande conjointe de partage, ils ne peuvent remettre en cause les modalités ainsi choisies qu'au bout d'un an, sauf modification des modalités de résidence du ou des enfants.
/*
# On ne modélise pas le délai minimal entre changement de situation dans
# ce programme.
*/

@Article R521-3@
Sous réserve de l'article R. 521-4, dans les situations visées aux 1° et 2° de l'article R. 521-2, la prestation due à chacun des parents est égale au montant des allocations familiales dues pour le total des enfants à charge, multiplié par un coefficient résultant du rapport entre le nombre moyen d'enfants et le nombre total d'enfants.

/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.avec_garde_alternée égal à
    allocations_familiales.base *
    allocations_familiales.rapport_enfants_total_moyen

  définition allocations_familiales.rapport_enfants_total_moyen égal à
    allocations_familiales.nombre_moyen_enfants /
    allocations_familiales.nombre_total_enfants
*/

Le nombre moyen d'enfants, pour chaque foyer, est obtenu en faisant la somme du nombre d'enfants à charge dans les conditions suivantes :
1° Chaque enfant en résidence alternée compte pour 0,5 ;
2° Les autres enfants à charge comptent pour 1.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.nombre_moyen_enfants égal à
    somme pour enfant dans ménage.enfants de (
      selon enfant.prise_en_charge sous forme
      -- Complète : 1
      -- GardeAlternée de parents : 0.5
    )
*/
Le nombre total d'enfants, pour chaque foyer, est obtenu en faisant la somme du ou des enfants en résidence alternée et, le cas échéant, du ou des autres enfants à charge.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.nombre_total_enfants égal à
    nombre de ménage.enfants
*/

@Article R521-4@
Pour l'ouverture du droit à la majoration prévue à l'article L. 521-3, le nombre d'enfants à charge est évalué dans les conditions prévues au premier alinéa de l'article R. 521-3.

/*
# ménage.enfants ne contient que des enfants à charge donc rien à formaliser
# ici
*/

Lorsque le ou les enfants ouvrant droit à ladite majoration sont en résidence alternée, le montant servi au titre de cette majoration est réduit de moitié.

/*
champ d'application CalculAllocationsFamiliales :
  définition
    majorations_allocations_familiales.avec_garde_alternée de enfant
  sous condition enfant dans ménage.enfants
  conséquence égal à (
    selon enfant.prise_en_charge sous forme
    -- Complète : majorations_allocations_familiales.base_par_enfant
    -- GardeAlternée de parents:
      majorations_allocations_familiales.base_par_enfant / 2
  )
*/

@Article D521-1@
I.- Pour l'application de l'article L. 521-1, le montant des allocations familiales et de la majoration pour âge prévue à l'article L. 521-3 est défini selon le barème suivant :
/*
# Composantes des allocations familiales
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.base sous condition
    allocations_familiales.droits_ouverts
  conséquence égal à
    allocations_familiales.montant_deuxieme_enfant +
    allocations_familiales.montant_troisième_enfant_et_plus
*/
1° Lorsque le ménage ou la personne a disposé d'un montant de ressources inférieur ou égal au plafond défini au I de l'article D. 521-3, les taux servant au calcul des allocations familiales sont fixés, en pourcentage de la base mensuelle prévue à l'article L. 551-1, à :
a) 32 % pour le deuxième enfant à charge ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_deuxieme_enfant sous condition
    l521_1.ressources_ménage <= d521_3.plafond_I
  conséquence égal à
    si nombre de ménage.enfants >= 2
    alors l551_1.base_mensuelle_prestations_familiales * 32 %
    sinon 0 €
*/
b) 41 % pour le troisième enfant à charge et chacun des suivants.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_troisième_enfant_et_plus
  sous condition
    l521_1.ressources_ménage <= d521_3.plafond_I
  conséquence égal à
    si nombre de ménage.enfants >= 3
    alors l551_1.base_mensuelle_prestations_familiales * 41 % * (
      (nombre de ménage.enfants) - 2
    ) sinon 0 €
*/
La majoration pour âge est fixée à 16 % de la base mensuelle de calcul des prestations familiales ;
/*
champ d'application CalculAllocationsFamiliales :
   définition
     majorations_allocations_familiales.base_par_enfant de enfant
   sous condition
     l521_1.ressources_ménage <= d521_3.plafond_I
   conséquence égal à
     l551_1.base_mensuelle_prestations_familiales * 16 %
*/
2° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au I de l'article D. 521-3 et inférieur ou égal à celui défini au II du même article, les taux servant au calcul des allocations familiales sont fixés, en pourcentage de la base mensuelle prévue à l'article L. 551-1, à :
a) 16 % pour le deuxième enfant à charge ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_deuxieme_enfant sous condition
    (l521_1.ressources_ménage > d521_3.plafond_I) et
    (l521_1.ressources_ménage <= d521_3.plafond_II)
  conséquence égal à
    si nombre de ménage.enfants >= 2
    alors l551_1.base_mensuelle_prestations_familiales * 32 %
    sinon 0 €
*/
b) 20,5 % pour le troisième enfant à charge et chacun des suivants.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_troisième_enfant_et_plus
  sous condition
    (l521_1.ressources_ménage > d521_3.plafond_I) et
    (l521_1.ressources_ménage <= d521_3.plafond_II)
  conséquence égal à
    si nombre de ménage.enfants >= 3
    alors l551_1.base_mensuelle_prestations_familiales * 20.5 % * (
      (nombre de ménage.enfants) - 2
    ) sinon 0 €
*/
La majoration pour âge est fixée à 8 % de la base mensuelle de calcul des prestations familiales ;
/*
champ d'application CalculAllocationsFamiliales :
   définition
     majorations_allocations_familiales.base_par_enfant de enfant
   sous condition
     (l521_1.ressources_ménage > d521_3.plafond_I) et
     (l521_1.ressources_ménage <= d521_3.plafond_II)
   conséquence égal à
     l551_1.base_mensuelle_prestations_familiales * 8 %
*/

3° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au II de l'article D. 521-3, les taux servant au calcul des allocations familiales sont fixés, en pourcentage de la base mensuelle prévue à l'article L. 551-1, à :
a) 8 % pour le deuxième enfant à charge ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_deuxieme_enfant sous condition
    (l521_1.ressources_ménage > d521_3.plafond_II)
  conséquence égal à
    si nombre de ménage.enfants >= 2
    alors l551_1.base_mensuelle_prestations_familiales * 8 %
    sinon 0 €
*/
b) 10,25 % pour le troisième enfant à charge et chacun des suivants.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocations_familiales.montant_troisième_enfant_et_plus
  sous condition
    (l521_1.ressources_ménage > d521_3.plafond_II)
  conséquence égal à
    si nombre de ménage.enfants >= 3
    alors l551_1.base_mensuelle_prestations_familiales * 10.25 % * (
      (nombre de ménage.enfants) - 2
    ) sinon 0 €
*/
La majoration pour âge est fixée à 4 % de la base mensuelle de calcul des prestations familiales.
/*
champ d'application CalculAllocationsFamiliales :
   définition
     majorations_allocations_familiales.base_par_enfant de enfant
   sous condition
     (l521_1.ressources_ménage > d521_3.plafond_II)
   conséquence égal à
     l551_1.base_mensuelle_prestations_familiales * 4 %
*/

II.- En application du sixième alinéa de l'article L. 521-1, le montant mensuel des allocations familiales et, le cas échéant, de la majoration pour âge est majoré d'un complément dégressif, lorsque les ressources annuelles du ménage ou de la personne dépassent l'un des plafonds défini au I ou au II de l'article D. 521-3 d'une somme inférieure à douze fois le montant mensuel des allocations familiales augmenté, le cas échéant, de la ou des majorations pour âge.

Ce complément dégressif est égal, pour chaque mois, au douzième de la différence entre, d'une part, ce plafond de ressources majoré de la somme définie à l'alinéa précédent et, d'autre part, le montant des ressources.
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_1.complément_dégressif de allocation
    sous condition
      (l521_1.ressources_ménage > d521_3.plafond_I) et
      (l521_1.ressources_ménage <= d521_3.plafond_I + 12 * allocation)
    conséquence égal à
      (d521_3.plafond_I + 12 * allocation - l521_1.ressources_ménage) / 12

  définition d521_1.complément_dégressif de allocation
    sous condition
      (l521_1.ressources_ménage > d521_3.plafond_II) et
      (l521_1.ressources_ménage <= d521_3.plafond_II + 12 * allocation)
    conséquence égal à
      (d521_3.plafond_II + 12 * allocation - l521_1.ressources_ménage) / 12

  # Dans tous les autres cas, le complément est de 0 €
  définition d521_1.complément_dégressif de allocation égal à 0 €

  définition allocations_familiales.avec_garde_alternée_et_complément égal à
    allocations_familiales.avec_garde_alternée +
    d521_1.complément_dégressif de
       allocations_familiales.avec_garde_alterné

  définition
    majorations_allocations_familiales.avec_garde_alternée_et_complément
  égal à
    majorations_allocations_familiales.avec_garde_alternée +
    d521_1.complément_dégressif de
      majorations_allocations_familiales.avec_garde_alternée
*/

@Article D521-2@
I.- Le montant mensuel de l'allocation forfaitaire prévue au deuxième alinéa de l'article L. 521-1 est défini selon le barème suivant :

1° Lorsque le ménage ou la personne a disposé d'un montant de ressources inférieur ou égal au plafond défini au I de l'article D. 521-3, le montant mensuel de l'allocation forfaitaire est fixé à 20,234 % de la base mensuelle de calcul des allocations familiales par enfant ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocation_forfaitaire.base sous condition
    l521_1.ressources_ménage <= d521_3.plafond_I
  conséquence égal à
    l551_1.base_mensuelle_prestations_familiales * 20.234 % *
    nombre pour enfant dans ménage.enfants de
      allocation_forfaitaire.droits_ouverts de enfant
*/

2° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au I de l'article D. 521-3 et inférieur ou égal à celui défini au II du même article, le montant mensuel de l'allocation forfaitaire est fixé à 10,117 % de la base mensuelle de calcul des allocations familiales par enfant ;
/*
champ d'application CalculAllocationsFamiliales :
  définition allocation_forfaitaire.base sous condition
    (l521_1.ressources_ménage > d521_3.plafond_I) et
    (l521_1.ressources_ménage <= d521_3.plafond_II)
  conséquence égal à
    l551_1.base_mensuelle_prestations_familiales * 10.117 % *
    nombre pour enfant dans ménage.enfants de
      allocation_forfaitaire.droits_ouvert de enfant
*/

3° Lorsque le ménage ou la personne a disposé d'un montant de ressources supérieur au plafond défini au II de l'article D. 521-3, le montant mensuel de l'allocation forfaitaire est fixé à 5,059 % de la base mensuelle de calcul des allocations familiales par enfant.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocation_forfaitaire.base sous condition
    l521_1.ressources_ménage > d521_3.plafond_II
  conséquence égal à
    l551_1.base_mensuelle_prestations_familiales * 5.059 % *
    nombre pour enfant dans ménage.enfants de
      allocation_forfaitaire.droits_ouvert de enfant
*/

II.- En application du sixième alinéa de l'article L. 521-1, le montant mensuel de l'allocation forfaitaire est majoré d'un complément dégressif lorsque les ressources annuelles du ménage ou de la personne dépassent l'un des plafonds défini au I ou au II de l'article D. 521-3 d'une somme inférieure à douze fois le montant mensuel de l'allocation forfaitaire auquel l'enfant ouvre droit.
Ce complément dégressif est égal, pour chaque mois, au douzième de la différence entre, d'une part, ce plafond de ressources majoré de la somme définie à l'alinéa précédent et, d'autre part, le montant des ressources.
/*
champ d'application CalculAllocationsFamiliales :
  définition allocation_forfaitaire.avec_complément égal à
    allocation_forfaitaire.base +
    d521_1.complément_dégressif de allocation_forfaitaire.base
*/

III.- Le nombre minimum d'enfants à charge mentionné au deuxième alinéa de l'article L. 521-1 est fixé à trois.
/*
champ d'application CalculAllocationsFamiliales :
  définition l512_1.nombre_minimum_enfants égal à 3
*/

@Article D521-3@

I.- Le plafond prévu au 1° du I des articles D. 521-1 et D. 521-2 est fixé à 55 950 euros. Il est majoré de 5 595 euros par enfant à charge.
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_3.plafond_I égal à 55950 € +
    5595 € * nombre de ménage.enfants
*/

II.- Le plafond prévu au 2° du I des articles D. 521-1 et D. 521-2 est fixé à 78 300 euros. Il est majoré de 5 595 euros par enfant à charge.
/*
champ d'application CalculAllocationsFamiliales :
  définition d521_3.plafond_II égal à 78300 € +
    5595 € * nombre de ménage.enfants
*/

III.- Les montants des plafonds et de leur majoration respective fixés au présent article sont revalorisés au 1er janvier de chaque année, conformément à l'évolution en moyenne annuelle des prix à la consommation hors tabac de l'année civile de référence, par arrêté des ministres chargés de la sécurité sociale, du budget et de l'agriculture.
